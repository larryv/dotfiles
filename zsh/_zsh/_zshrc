# zsh/_zsh/zshrc
# --------------
#
# SPDX-License-Identifier: CC0-1.0
#
# Written in 2012-2016, 2018-2023 by Lawrence Velazquez <vq@larryv.me>.
#
# To the extent possible under law, the author has dedicated all
# copyright and related and neighboring rights to this software to the
# public domain worldwide.  This software is distributed without any
# warranty.
#
# You should have received a copy of the CC0 Public Domain Dedication
# along with this software.  If not, see
# <https://creativecommons.org/publicdomain/zero/1.0/>.


# TODO: If compatible with 4.3.6, move everything into a prompt theme.

# Left prompt: Hostname, history number, job count, indication of last
# exit status, privileges/shell-nesting indicator.
PS1='%m %B%h%b%(1j. [%j].)'
if
	# This expansion should be null if %f is unsupported.  (Color
	# prompt escapes were introduced in zsh 4.3.7; I used to
	# accommodate older versions but can't be bothered anymore.)
	[[ ${(%):-%f} ]]
then
	# Prefer the numbered form of %F because it uses termcap instead
	# of hard-coded ANSI sequences [1].
	PS1+=" %(?.%2F.%1F)%B${(r:SHLVL * 2::%#:)}%b%f "
else
	# Display the last exit status directly.
	PS1+="%(?..%(1j.. )(%?%)) %B${(r:SHLVL * 2::%#:)}%b "
fi

# Leave the -g in case I ever wrap this in a function again.
typeset -ag precmd_functions

# Right prompt: Current working directory (truncated to half screen
# width) and version control info.
precmd_rps_cwd() {
	RPS1="%B%$((COLUMNS / 2))<..<%~%b"
}
precmd_functions+=(precmd_rps_cwd)
autoload -Uz +X vcs_info 2>/dev/null && {
	precmd_rps_vcs() {
		# TODO Rework RPS1 to use %v and psvar [2]?
		local vcs_info_msg_0_
		vcs_info && RPS1+=${vcs_info_msg_0_:+ $vcs_info_msg_0_}
	}
	precmd_functions+=(precmd_rps_vcs)

	zstyle ':vcs_info:*' enable git hg svn

	zstyle ':vcs_info:*' formats '(%s:%b)'
	zstyle ':vcs_info:*' actionformats '(%s:%b|%a)'
	zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat '%b/%r'

	zstyle ':vcs_info:git:*' check-for-changes true
	if [[ ${(%):-%f} ]]
	then
		# Color green/yellow/red if there are no/staged/unstaged changes.
		zstyle ':vcs_info:git:*' unstagedstr '%1F'
		zstyle ':vcs_info:git:*' stagedstr '%3F'
		zstyle ':vcs_info:git:*' formats '%2F%u%c(%s:%b)%f'
		zstyle ':vcs_info:git:*' actionformats '%2F%u%c(%s:%b|%a)%f'
	else
		zstyle ':vcs_info:git:*' unstagedstr ':U'
		zstyle ':vcs_info:git:*' stagedstr ':S'
		zstyle ':vcs_info:git:*' formats '(%s:%b%u%c)'
		zstyle ':vcs_info:git:*' actionformats '(%s:%b%u%c|%a)'
	fi
}

# Send current working directory to the terminal emulator.
autoload -Uz +X update_terminal_cwd 2>/dev/null && {
	precmd_functions+=(update_terminal_cwd)
}

# Enable completion and other things.
autoload -Uz compinit run-help zargs zrecompile
if [[ ${ZDOTDIR-~} != ~ ]]
then
	compinit -d "$ZDOTDIR"/zcompdump
else
	compinit
fi && {
	zstyle ':completion:*:descriptions' format '%B%d%b'
	# TODO: Make sure %1F and %f expand to null in zsh 4.3.6.
	zstyle ':completion:*:warnings' format '%1F%BNo matches for %d%b%f'
}

# Enable fancy globbing.
setopt EXTENDED_GLOB

# History.
SAVEHIST=1000000
HISTSIZE=$SAVEHIST
if (( $+ZDOTDIR ))
then
	HISTFILE=$ZDOTDIR/zsh_history
else
	HISTFILE=~/.zsh_history
fi
setopt EXTENDED_HISTORY
# < 5.0.6: Just append history at exit, it's fine.
setopt INC_APPEND_HISTORY_TIME 2>/dev/null

# Print timing stats for commands that use over 10 seconds of CPU time.
REPORTTIME=10

# Source topic scripts.
for scpt in "${ZDOTDIR-$HOME}"/zshrc.d/*.zsh(N-^/)
do
	. "$scpt"
done
unset scpt

# Login shells will do this later.
if [[ ! -o LOGIN ]]
then
	if [[ $tmpfuncs ]]
	then
		unfunction ${(u)=tmpfuncs}
	fi
	unset srcd tmpfuncs
fi


# References
#
#  1. https://sourceforge.net/p/zsh/code/ci/zsh-4.3.7/tree/Src/prompt.c
#  2. https://www.zsh.org/mla/workers/2021/msg00811.html

# vim: set filetype=zsh:
