# zsh/_zsh/zshrc
# --------------
#
# SPDX-License-Identifier: CC0-1.0
#
# Written in 2012-2016, 2018-2023 by Lawrence Velazquez <vq@larryv.me>.
#
# To the extent possible under law, the author(s) have dedicated all
# copyright and related and neighboring rights to this software to the
# public domain worldwide.  This software is distributed without any
# warranty.
#
# You should have received a copy of the CC0 Public Domain Dedication
# along with this software.  If not, see
# <https://creativecommons.org/publicdomain/zero/1.0/>.


# >= 4.3.7: %1F, %2F, %3F, %f (numbered form uses termcap [1])
# Avoid using Functions/Misc/colors, which hard-codes ANSI sequences and
# might not be installed.  Define my own colors instead.
# NOTE: This function creates the "fgbg" global variable.
autoload -Uz colorpairs
colorpairs -f red,green,yellow,default -b default

# TODO: If compatible with 4.3.6, move everything into a prompt theme.

# Left prompt: Hostname, history number, job count, indication of last
# exit status, privileges/shell-nesting indicator.
PS1='%m %B%h%b%(1j. [%j].)'
if (( $#fgbg )); then
    PS1+=" %(?.%{$fgbg[green,default]%}.%{$fgbg[red,default]%})"
    PS1+="%B${(r:$((SHLVL * 2))::%#:)}%b"
    PS1+="%{$fgbg[default,default]%} "
else
    # Display the last exit status directly.
    PS1+="%(?..%(1j.. )(%?%)) %B${(r:$((SHLVL * 2))::%#:)}%b "
fi

# Leave the -g in case I ever wrap this in a function again.
typeset -ag precmd_functions

# Right prompt: Current working directory (truncated to half screen
# width) and version control info.
precmd_rps_cwd() {
    RPS1="%B%$((COLUMNS / 2))<..<%~%b"
}
precmd_functions+=(precmd_rps_cwd)
autoload -Uz +X vcs_info 2>/dev/null && {
    precmd_rps_vcs() {
        # TODO Rework RPS1 to use %v and psvar [2]?
        local vcs_info_msg_0_
        vcs_info && RPS1+=${vcs_info_msg_0_:+ ${vcs_info_msg_0_}}
    }
    precmd_functions+=(precmd_rps_vcs)

    zstyle ':vcs_info:*' enable git hg svn

    zstyle ':vcs_info:*' formats '(%s:%b)'
    zstyle ':vcs_info:*' actionformats '(%s:%b|%a)'
    zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat '%b/%r'

    zstyle ':vcs_info:git:*' check-for-changes true
    if (( $#fgbg )); then
        # Color green/yellow/red if there are no/staged/unstaged changes.
        zstyle ':vcs_info:git:*' unstagedstr "%{$fgbg[red,default]%}"
        zstyle ':vcs_info:git:*' stagedstr "%{$fgbg[yellow,default]%}"
        zstyle ':vcs_info:git:*' formats \
            "%{$fgbg[green,default]%}%u%c(%s:%b)%{$fgbg[default,default]%}"
        zstyle ':vcs_info:git:*' actionformats \
            "%{$fgbg[green,default]%}%u%c(%s:%b|%a)%{$fgbg[default,default]%}"
    else
        zstyle ':vcs_info:git:*' unstagedstr ':U'
        zstyle ':vcs_info:git:*' stagedstr ':S'
        zstyle ':vcs_info:git:*' formats '(%s:%b%u%c)'
        zstyle ':vcs_info:git:*' actionformats '(%s:%b%u%c|%a)'
    fi
}

# Send current working directory to the terminal emulator.
autoload -Uz +X update_terminal_cwd 2>/dev/null && {
    precmd_functions+=(update_terminal_cwd)
}

# Enable completion and other things.
autoload -Uz run-help zargs zrecompile
autoload -Uz compinit && compinit -d $ZDOTDIR/zcompdump && {
    zstyle ':completion:*:descriptions' format '%B%d%b'

    fmt='%BNo matches for %d%b'
    if (( $#fgbg )); then
        fmt="%{$fgbg[red,default]%}$fmt%{$fgbg[default,default]%}"
    fi
    zstyle ':completion:*:warnings' format $fmt
    unset fmt
}

# Enable fancy globbing.
setopt EXTENDED_GLOB

# History.
SAVEHIST=1000000
HISTSIZE=$SAVEHIST
HISTFILE=$ZDOTDIR/zsh_history
setopt EXTENDED_HISTORY
# < 5.0.6: Just append history at exit, it's fine.
setopt INC_APPEND_HISTORY_TIME 2>/dev/null

# Print timing stats for commands that use over 10 seconds of CPU time.
REPORTTIME=10

# Source topic scripts.
for script in $ZDOTDIR/zshrc.d/*.zsh(N-^/); do
    . $script
done
unset script

# Login shells will do this later.
if [[ ! -o LOGIN ]]; then
    unset_sh_helper_functions 2>/dev/null
    unfunction emulated_eval unset_sh_helper_functions 2>/dev/null
    unset sourced_scripts
fi

unset fgbg
unfunction colorpairs


# References
#
#  1. https://sourceforge.net/p/zsh/code/ci/zsh-4.3.7/tree/Src/prompt.c
#  2. https://www.zsh.org/mla/workers/2021/msg00811.html
